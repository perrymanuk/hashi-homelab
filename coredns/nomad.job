job "coredns" {
  region = var.region
  datacenters = ["dc1"]
  type        = "system"
  priority    = 100

  meta {
    version = "2"
  }

  constraint {
    attribute = "${meta.dns}"
    operator  = "="
    value     = "true"
  }

  group "infrastructure" {
    count = 1 

    network {
      mode = "host"
      port "dns" {
        static = "53"
        host_network = "lan"
      }
      port "metrics" {
        static = "9153"
        host_network = "tailscale"
      }
    }

    task "coredns" {
      driver = "docker"
      config {
        image = "coredns/coredns:1.10.1"
        network_mode = "host"
        ports = ["dns", "metrics"]
        args = ["-conf", "local/coredns/corefile"]
      }

      service {
        port = "dns"
	name = "coredns"
        tags = ["coredns"]
        check {
          type     = "tcp"
          interval = "10s"
          timeout  = "2s"
        }
      }
      service {
        port = "metrics"
	name = "coredns"
        tags = ["metrics", "coredns"]
      }

      template {
data = <<EOH
. {
  bind {{ env "NOMAD_IP_dns" }} 192.168.50.2 
  {{ range service "pihole" }}forward . {{ .Address }}:8053{{ end }}
  log
  errors
  prometheus {{ env "NOMAD_IP_metrics" }}:9153
}
home.:53 {
  bind {{ env "NOMAD_IP_dns" }} 192.168.50.2 
  forward . {{ sockaddr "GetInterfaceIP \"tailscale0\"" }}:8600
  log
  prometheus {{ env "NOMAD_IP_metrics" }}:9153
}
EOH
        destination = "local/coredns/corefile"
        env         = false
        change_mode = "signal"
        change_signal = "SIGHUP"
        left_delimiter  = "{{"
        right_delimiter = "}}"

      }

      resources {
        cpu    = 100
        memory = 128
      }
    }

    task "keepalived" {
      driver = "docker"
      config {
        image = "osixia/keepalived:2.0.20"
        network_mode = "host"
        volumes = [
            "local/:/container/environment/01-custom"
        ]
        cap_add = ["NET_ADMIN", "NET_BROADCAST", "NET_RAW"]
      }
      template {
        destination = "local/env.yaml"
        change_mode = "restart"
        splay       = "1m"
        data        = <<EOH
KEEPALIVED_VIRTUAL_IPS:
  - 192.168.50.2/32
KEEPALIVED_UNICAST_PEERS:
{{- with $node := node -}}
{{ range nodes }}
{{- if ne .Address $node.Node.Address }}
  - {{ .Address }}
{{- end -}}
{{- end -}}
{{- end }}
KEEPALIVED_PRIORITY: {{ env "meta.keepalived_priority" }}
KEEPALIVED_INTERFACE: {{ sockaddr "GetPrivateInterfaces | include \"network\" \"192.168.50.0/24\" | attr \"name\"" }}
EOH
      }

      resources {
        cpu    = 10
        memory = 12
      }
    }
  }
}

variable "region" {}


