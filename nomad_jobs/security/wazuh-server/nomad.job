job "wazuh-server" {
  region      = var.region
  datacenters = ["dc1"]
  type        = "service"

  constraint {
    attribute = "${meta.shared_mount}"
    operator  = "="
    value     = "true"
  }

  group "wazuh-stack" {
    count = 1

    network {
      port "indexer" {
        host_network = "lan"
        to = 9200
      }
      port "manager" {
        static       = 1514
        host_network = "lan"
        to = 1514
      }
      port "manager_reg" {
        static       = 1515
        host_network = "lan"
        to = 1515
      }
      port "manager_api" {
        host_network = "lan"
        to = 55000
      }
      port "dashboard" {
        host_network = "lan"
        to = 443
      }
    }

    # Persistent volumes for Wazuh components
    volume "wazuh-indexer" {
      type            = "csi"
      read_only       = false
      source          = "wazuh-indexer"
      access_mode     = "single-node-writer"
      attachment_mode = "file-system"
    }

    volume "wazuh-manager" {
      type            = "csi"
      read_only       = false
      source          = "wazuh-manager"
      access_mode     = "single-node-writer"
      attachment_mode = "file-system"
    }

    volume "wazuh-dashboard" {
      type            = "csi"
      read_only       = false
      source          = "wazuh-dashboard"
      access_mode     = "single-node-writer"
      attachment_mode = "file-system"
    }

    # Prep disk task for indexer volume permissions
    task "prep-indexer-disk" {
      driver = "docker"

      volume_mount {
        volume      = "wazuh-indexer"
        destination = "/volume/"
        read_only   = false
      }

      config {
        image   = "busybox:latest"
        command = "sh"
        args    = ["-c", "chown -R 1000:1000 /volume/"]
      }

      resources {
        cpu    = 200
        memory = 128
      }

      lifecycle {
        hook    = "prestart"
        sidecar = false
      }
    }

    # Prep disk task for manager volume permissions
    task "prep-manager-disk" {
      driver = "docker"

      volume_mount {
        volume      = "wazuh-manager"
        destination = "/volume/"
        read_only   = false
      }

      config {
        image   = "busybox:latest"
        command = "sh"
        args    = ["-c", "chown -R 999:999 /volume/"]
      }

      resources {
        cpu    = 200
        memory = 128
      }

      lifecycle {
        hook    = "prestart"
        sidecar = false
      }
    }


    # Prep disk task for dashboard volume permissions
    task "prep-dashboard-disk" {
      driver = "docker"

      volume_mount {
        volume      = "wazuh-dashboard"
        destination = "/volume/"
        read_only   = false
      }

      config {
        image   = "busybox:latest"
        command = "sh"
        args    = ["-c", "rm -rf /volume/wazuh && mkdir -p /volume/wazuh/config && chown -R 1000:1000 /volume/"]
      }

      resources {
        cpu    = 200
        memory = 128
      }

      lifecycle {
        hook    = "prestart"
        sidecar = false
      }
    }

    # Wazuh Indexer (OpenSearch-based)
    task "wazuh-indexer" {
      driver = "docker"

      volume_mount {
        volume      = "wazuh-indexer"
        destination = "/var/lib/wazuh-indexer"
        read_only   = false
      }

      config {
        image      = "wazuh/wazuh-indexer:4.14.2"
        force_pull = true
        ports      = ["indexer"]
        volumes = [
          "local/opensearch.yml:/usr/share/wazuh-indexer/config/opensearch.yml",
        ]
        ulimit {
          nofile = "65536:65536"
          memlock = "-1:-1"
        }
      }

      env {
        OPENSEARCH_JAVA_OPTS = "-Xms1g -Xmx1g"
      }

      template {
        data = <<EOH
network.host: "0.0.0.0"
node.name: "wazuh-indexer"
cluster.name: "wazuh-cluster"
path.data: /var/lib/wazuh-indexer
path.logs: /var/log/wazuh-indexer
discovery.type: single-node
compatibility.override_main_response_version: true

# Enable SSL with demo certificates
plugins.security.ssl.http.enabled: true
plugins.security.ssl.http.pemcert_filepath: certs/indexer.pem
plugins.security.ssl.http.pemkey_filepath: certs/indexer-key.pem
plugins.security.ssl.http.pemtrustedcas_filepath: certs/root-ca.pem
plugins.security.ssl.transport.enabled: true
plugins.security.ssl.transport.pemcert_filepath: certs/indexer.pem
plugins.security.ssl.transport.pemkey_filepath: certs/indexer-key.pem
plugins.security.ssl.transport.pemtrustedcas_filepath: certs/root-ca.pem
plugins.security.ssl.transport.enforce_hostname_verification: false
plugins.security.ssl.transport.resolve_hostname: false
plugins.security.allow_unsafe_democertificates: true
plugins.security.allow_default_init_securityindex: true
plugins.security.authcz.admin_dn:
  - "CN=admin,OU=Wazuh,O=Wazuh,L=California,C=US"
plugins.security.nodes_dn:
  - "CN=wazuh-indexer,OU=Wazuh,O=Wazuh,L=California,C=US"
plugins.security.audit.type: internal_opensearch
plugins.security.enable_snapshot_restore_privilege: true
plugins.security.check_snapshot_restore_write_privileges: true
plugins.security.restapi.roles_enabled: ["all_access", "security_rest_api_access"]
EOH
        destination = "local/opensearch.yml"
        perms       = "0644"
      }

      resources {
        cpu    = 1000
        memory = 2048
      }

      service {
        name = "wazuh-indexer"
        port = "indexer"

        check {
          type     = "tcp"
          port     = "indexer"
          interval = "10s"
          timeout  = "2s"
        }
      }
    }

    # Wazuh Manager (Analysis Engine)
    task "wazuh-manager" {
      driver = "docker"

      volume_mount {
        volume      = "wazuh-manager"
        destination = "/var/ossec/data"
        read_only   = false
      }

      config {
        image      = "wazuh/wazuh-manager:4.14.2"
        force_pull = true
        hostname   = "wazuh-manager"
        ports      = ["manager", "manager_reg", "manager_api"]
        volumes = [
          "local/local_rules.xml:/var/ossec/etc/rules/local_rules.xml:ro",
          "local/ossec.conf:/var/ossec/etc/ossec.conf:ro",
        ]
      }

      template {
        data = <<EOH
INDEXER_URL=https://{{ env "NOMAD_IP_indexer" }}:{{ env "NOMAD_HOST_PORT_indexer" }}
INDEXER_USERNAME=admin
INDEXER_PASSWORD=admin
FILEBEAT_SSL_VERIFICATION_MODE=none
SSL_CERTIFICATE_AUTHORITIES=""
API_USERNAME=wazuh-wui
API_PASSWORD=${var.wazuh_api_password}
EOH
        destination = "secrets/manager.env"
        env         = true
      }

      # Custom rules to suppress noisy alerts
      template {
        data = <<EOH
<!-- Local rules for Wazuh Manager -->
<group name="local,syslog,suricata,">

  <!-- Suppress noisy Suricata STREAM alerts (rule 86601) -->
  <!-- These "invalid ack" alerts are low-value network noise -->
  <rule id="100001" level="0">
    <if_sid>86601</if_sid>
    <description>Suppressed: Suricata STREAM ESTABLISHED invalid ack</description>
  </rule>

</group>
EOH
        destination = "local/local_rules.xml"
        perms       = "0644"
      }

      # Wazuh ossec.conf with log_alert_level=8 (only high/critical alerts)
      # Wazuh levels: 0=ignored, 1-4=low, 5-7=medium, 8-10=high, 11-15=critical
      template {
        data = <<EOH
<!--
  Wazuh - Manager - Default configuration for amzn 2023
  More info at: https://documentation.wazuh.com
  Mailing list: https://groups.google.com/forum/#!forum/wazuh

  CUSTOMIZED: log_alert_level set to 8 (high/critical alerts only)
-->

<ossec_config>
  <global>
    <jsonout_output>yes</jsonout_output>
    <alerts_log>yes</alerts_log>
    <logall>no</logall>
    <logall_json>no</logall_json>
    <email_notification>no</email_notification>
    <smtp_server>smtp.example.wazuh.com</smtp_server>
    <email_from>wazuh@example.wazuh.com</email_from>
    <email_to>recipient@example.wazuh.com</email_to>
    <email_maxperhour>12</email_maxperhour>
    <email_log_source>alerts.log</email_log_source>
    <agents_disconnection_time>15m</agents_disconnection_time>
    <agents_disconnection_alert_time>0</agents_disconnection_alert_time>
    <update_check>yes</update_check>
  </global>

  <alerts>
    <!-- CUSTOMIZED: Changed from 3 to 8 to only log high/critical alerts -->
    <log_alert_level>8</log_alert_level>
    <email_alert_level>12</email_alert_level>
  </alerts>

  <!-- Choose between "plain", "json", or "plain,json" for the format of internal logs -->
  <logging>
    <log_format>plain</log_format>
  </logging>

  <remote>
    <connection>secure</connection>
    <port>1514</port>
    <protocol>tcp</protocol>
    <queue_size>131072</queue_size>
  </remote>

  <!-- Policy monitoring -->
  <rootcheck>
    <disabled>no</disabled>
    <check_files>yes</check_files>
    <check_trojans>yes</check_trojans>
    <check_dev>yes</check_dev>
    <check_sys>yes</check_sys>
    <check_pids>yes</check_pids>
    <check_ports>yes</check_ports>
    <check_if>yes</check_if>

    <!-- Frequency that rootcheck is executed - every 12 hours -->
    <frequency>43200</frequency>

    <rootkit_files>etc/rootcheck/rootkit_files.txt</rootkit_files>
    <rootkit_trojans>etc/rootcheck/rootkit_trojans.txt</rootkit_trojans>

    <skip_nfs>yes</skip_nfs>

    <ignore>/var/lib/containerd</ignore>
    <ignore>/var/lib/docker/overlay2</ignore>
  </rootcheck>

  <wodle name="cis-cat">
    <disabled>yes</disabled>
    <timeout>1800</timeout>
    <interval>1d</interval>
    <scan-on-start>yes</scan-on-start>

    <java_path>wodles/java</java_path>
    <ciscat_path>wodles/ciscat</ciscat_path>
  </wodle>

  <!-- Osquery integration -->
  <wodle name="osquery">
    <disabled>yes</disabled>
    <run_daemon>yes</run_daemon>
    <log_path>/var/log/osquery/osqueryd.results.log</log_path>
    <config_path>/etc/osquery/osquery.conf</config_path>
    <add_labels>yes</add_labels>
  </wodle>

  <!-- System inventory -->
  <wodle name="syscollector">
    <disabled>no</disabled>
    <interval>1h</interval>
    <scan_on_start>yes</scan_on_start>
    <hardware>yes</hardware>
    <os>yes</os>
    <network>yes</network>
    <packages>yes</packages>
    <ports all="yes">yes</ports>
    <processes>yes</processes>
    <users>yes</users>
    <groups>yes</groups>
    <services>yes</services>
    <browser_extensions>yes</browser_extensions>

    <!-- Database synchronization settings -->
    <synchronization>
      <max_eps>10</max_eps>
    </synchronization>
  </wodle>

  <sca>
    <enabled>yes</enabled>
    <scan_on_start>yes</scan_on_start>
    <interval>12h</interval>
    <skip_nfs>yes</skip_nfs>
  </sca>

  <vulnerability-detection>
    <enabled>yes</enabled>
    <index-status>yes</index-status>
    <feed-update-interval>60m</feed-update-interval>
  </vulnerability-detection>

  <indexer>
    <enabled>yes</enabled>
    <hosts>
      <host>https://{{ env "NOMAD_IP_indexer" }}:{{ env "NOMAD_HOST_PORT_indexer" }}</host>
    </hosts>
    <ssl>
      <certificate_authorities>
        <ca>/etc/filebeat/certs/root-ca.pem</ca>
      </certificate_authorities>
      <certificate>/etc/filebeat/certs/filebeat.pem</certificate>
      <key>/etc/filebeat/certs/filebeat-key.pem</key>
    </ssl>
  </indexer>

  <!-- File integrity monitoring -->
  <syscheck>
    <disabled>no</disabled>

    <!-- Frequency that syscheck is executed default every 12 hours -->
    <frequency>43200</frequency>

    <scan_on_start>yes</scan_on_start>

    <!-- Generate alert when new file detected -->
    <alert_new_files>yes</alert_new_files>

    <!-- Don't ignore files that change more than 'frequency' times -->
    <auto_ignore frequency="10" timeframe="3600">no</auto_ignore>

    <!-- Directories to check  (perform all possible verifications) -->
    <directories>/etc,/usr/bin,/usr/sbin</directories>
    <directories>/bin,/sbin,/boot</directories>

    <!-- Files/directories to ignore -->
    <ignore>/etc/mtab</ignore>
    <ignore>/etc/hosts.deny</ignore>
    <ignore>/etc/mail/statistics</ignore>
    <ignore>/etc/random-seed</ignore>
    <ignore>/etc/random.seed</ignore>
    <ignore>/etc/adjtime</ignore>
    <ignore>/etc/httpd/logs</ignore>
    <ignore>/etc/utmpx</ignore>
    <ignore>/etc/wtmpx</ignore>
    <ignore>/etc/cups/certs</ignore>
    <ignore>/etc/dumpdates</ignore>
    <ignore>/etc/svc/volatile</ignore>

    <!-- File types to ignore -->
    <ignore type="sregex">.log$|.swp$</ignore>

    <!-- Check the file, but never compute the diff -->
    <nodiff>/etc/ssl/private.key</nodiff>

    <skip_nfs>yes</skip_nfs>
    <skip_dev>yes</skip_dev>
    <skip_proc>yes</skip_proc>
    <skip_sys>yes</skip_sys>

    <!-- Nice value for Syscheck process -->
    <process_priority>10</process_priority>

    <!-- Maximum output throughput -->
    <max_eps>50</max_eps>

    <!-- Database synchronization settings -->
    <synchronization>
      <enabled>yes</enabled>
      <interval>5m</interval>
      <max_eps>10</max_eps>
    </synchronization>
  </syscheck>

  <!-- Active response -->
  <global>
    <white_list>127.0.0.1</white_list>
    <white_list>^localhost.localdomain$</white_list>
    <white_list>168.63.129.16</white_list>
  </global>

  <command>
    <name>disable-account</name>
    <executable>disable-account</executable>
    <timeout_allowed>yes</timeout_allowed>
  </command>

  <command>
    <name>restart-wazuh</name>
    <executable>restart-wazuh</executable>
  </command>

  <command>
    <name>firewall-drop</name>
    <executable>firewall-drop</executable>
    <timeout_allowed>yes</timeout_allowed>
  </command>

  <command>
    <name>host-deny</name>
    <executable>host-deny</executable>
    <timeout_allowed>yes</timeout_allowed>
  </command>

  <command>
    <name>route-null</name>
    <executable>route-null</executable>
    <timeout_allowed>yes</timeout_allowed>
  </command>

  <command>
    <name>win_route-null</name>
    <executable>route-null.exe</executable>
    <timeout_allowed>yes</timeout_allowed>
  </command>

  <command>
    <name>netsh</name>
    <executable>netsh.exe</executable>
    <timeout_allowed>yes</timeout_allowed>
  </command>

  <!--
  <active-response>
    active-response options here
  </active-response>
  -->

  <!-- Log analysis -->
  <localfile>
    <log_format>command</log_format>
    <command>df -P</command>
    <frequency>360</frequency>
  </localfile>

  <localfile>
    <log_format>full_command</log_format>
    <command>netstat -tulpn | sed 's/\([[:alnum:]]\+\)\ \+[[:digit:]]\+\ \+[[:digit:]]\+\ \+\(.*\):\([[:digit:]]*\)\ \+\([0-9\.\:\*]\+\).\+\ \([[:digit:]]*\/[[:alnum:]\-]*\).*/\1 \2 == \3 == \4 \5/' | sort -k 4 -g | sed 's/ == \(.*\) ==/:\1/' | sed 1,2d</command>
    <alias>netstat listening ports</alias>
    <frequency>360</frequency>
  </localfile>

  <localfile>
    <log_format>full_command</log_format>
    <command>last -n 20</command>
    <frequency>360</frequency>
  </localfile>

  <ruleset>
    <!-- Default ruleset -->
    <decoder_dir>ruleset/decoders</decoder_dir>
    <rule_dir>ruleset/rules</rule_dir>
    <rule_exclude>0215-policy_rules.xml</rule_exclude>
    <list>etc/lists/audit-keys</list>
    <list>etc/lists/amazon/aws-eventnames</list>
    <list>etc/lists/security-eventchannel</list>
    <list>etc/lists/malicious-ioc/malware-hashes</list>
    <list>etc/lists/malicious-ioc/malicious-ip</list>
    <list>etc/lists/malicious-ioc/malicious-domains</list>

    <!-- User-defined ruleset -->
    <decoder_dir>etc/decoders</decoder_dir>
    <rule_dir>etc/rules</rule_dir>
  </ruleset>

  <rule_test>
    <enabled>yes</enabled>
    <threads>1</threads>
    <max_sessions>64</max_sessions>
    <session_timeout>15m</session_timeout>
  </rule_test>

  <!-- Configuration for wazuh-authd -->
  <auth>
    <disabled>no</disabled>
    <port>1515</port>
    <use_source_ip>no</use_source_ip>
    <purge>yes</purge>
    <use_password>no</use_password>
    <ciphers>HIGH:!ADH:!EXP:!MD5:!RC4:!3DES:!CAMELLIA:@STRENGTH</ciphers>
    <!-- <ssl_agent_ca></ssl_agent_ca> -->
    <ssl_verify_host>no</ssl_verify_host>
    <ssl_manager_cert>etc/sslmanager.cert</ssl_manager_cert>
    <ssl_manager_key>etc/sslmanager.key</ssl_manager_key>
    <ssl_auto_negotiate>no</ssl_auto_negotiate>
  </auth>

  <cluster>
    <name>wazuh</name>
    <node_name>node01</node_name>
    <node_type>master</node_type>
    <key></key>
    <port>1516</port>
    <bind_addr>0.0.0.0</bind_addr>
    <nodes>
        <node>NODE_IP</node>
    </nodes>
    <hidden>no</hidden>
    <disabled>yes</disabled>
  </cluster>

</ossec_config>

<ossec_config>
  <localfile>
    <log_format>syslog</log_format>
    <location>/var/ossec/logs/active-responses.log</location>
  </localfile>

</ossec_config>
EOH
        destination = "local/ossec.conf"
        perms       = "0644"
      }

      resources {
        cpu    = 1000
        memory = 1024
      }

      service {
        name = "wazuh-manager"
        port = "manager_api"
        tags = ["metrics"]

        meta {
          api_port = "${NOMAD_HOST_PORT_manager_api}"
        }

        check {
          type     = "tcp"
          port     = "manager_api"
          interval = "10s"
          timeout  = "2s"
        }
      }

      service {
        name = "wazuh-agent-comm"
        port = "manager"
        tags = ["agent-communication"]

        check {
          type     = "tcp"
          port     = "manager"
          interval = "10s"
          timeout  = "2s"
        }
      }

      service {
        name = "wazuh-agent-reg"
        port = "manager_reg"
        tags = ["agent-registration"]

        check {
          type     = "tcp"
          port     = "manager_reg"
          interval = "10s"
          timeout  = "2s"
        }
      }
    }

    # Wazuh Dashboard (Web UI)
    task "wazuh-dashboard" {
      driver = "docker"

      volume_mount {
        volume      = "wazuh-dashboard"
        destination = "/usr/share/wazuh-dashboard/data"
        read_only   = false
      }

      config {
        image      = "wazuh/wazuh-dashboard:4.14.3"
        force_pull = true
        ports      = ["dashboard"]
        volumes = [
          "local/opensearch_dashboards.yml:/usr/share/wazuh-dashboard/config/opensearch_dashboards.yml",
          "local/wazuh.yml:/usr/share/wazuh-dashboard/data/wazuh/config/wazuh.yml:ro",
        ]
      }

      template {
        data = <<EOH
OPENSEARCH_HOSTS=https://{{ env "NOMAD_IP_indexer" }}:{{ env "NOMAD_HOST_PORT_indexer" }}
OPENSEARCH_USERNAME=admin
OPENSEARCH_PASSWORD=admin
OPENSEARCH_SSL_VERIFICATIONMODE=none
SERVER_HOST=0.0.0.0
SERVER_PORT=443
SERVER_SSL_ENABLED=true
SERVER_SSL_CERTIFICATE=/usr/share/wazuh-dashboard/config/certs/dashboard.pem
SERVER_SSL_KEY=/usr/share/wazuh-dashboard/config/certs/dashboard-key.pem
WAZUH_API_URL=https://{{ env "NOMAD_IP_manager_api" }}:{{ env "NOMAD_HOST_PORT_manager_api" }}
API_USERNAME=wazuh-wui
API_PASSWORD=${var.wazuh_api_password}
EOH
        destination = "secrets/dashboard.env"
        env         = true
        change_mode = "noop"
      }

      template {
        data = <<EOH
server.host: "0.0.0.0"
server.port: 443
opensearch.hosts: ["https://{{ env "NOMAD_IP_indexer" }}:{{ env "NOMAD_HOST_PORT_indexer" }}"]
opensearch.ssl.verificationMode: none
opensearch.username: "admin"
opensearch.password: "admin"
opensearch.requestHeadersWhitelist: ["securitytenant","Authorization"]
opensearch_security.multitenancy.enabled: false
opensearch_security.readonly_mode.roles: ["kibana_read_only"]
server.ssl.enabled: true
server.ssl.certificate: /usr/share/wazuh-dashboard/config/certs/dashboard.pem
server.ssl.key: /usr/share/wazuh-dashboard/config/certs/dashboard-key.pem
uiSettings.overrides.defaultRoute: "/app/wz-home"
csp.warnLegacyBrowsers: false
EOH
        destination = "local/opensearch_dashboards.yml"
        perms       = "0644"
        change_mode = "noop"
      }

      template {
        data = <<EOH
hosts:
  - default:
      url: https://{{ env "NOMAD_IP_manager_api" }}
      port: {{ env "NOMAD_HOST_PORT_manager_api" }}
      username: wazuh-wui
      password: ${var.wazuh_api_password}
      run_as: false
EOH
        destination = "local/wazuh.yml"
        perms       = "0644"
        change_mode = "noop"
      }

      resources {
        cpu    = 500
        memory = 1024
      }

      service {
        name = "wazuh-dashboard"
        port = "dashboard"
        tags = [
          "traefik.enable=true",
          "traefik.http.routers.wazuh.rule=Host(`wazuh.${var.tld}`)",
          "traefik.http.routers.wazuh.entrypoints=websecure",
          "traefik.http.routers.wazuh.tls=true",
          "traefik.http.routers.wazuh.tls.certresolver=letsencrypt",
          "traefik.http.services.wazuh.loadbalancer.server.scheme=https",
          "traefik.http.services.wazuh.loadbalancer.serversTransport=insecure-skip-verify@file",
        ]

        check {
          type     = "tcp"
          port     = "dashboard"
          interval = "10s"
          timeout  = "2s"
        }
      }
    }

  }
}

variable "region" {
  type = string
}

variable "tld" {
  type = string
}

variable "shared_dir" {
  type = string
}

variable "wazuh_indexer_password" {
  type = string
  description = "Password for Wazuh indexer admin user"
}

variable "wazuh_api_password" {
  type = string
  description = "Password for Wazuh API user"
}

variable "wazuh_dashboard_password" {
  type = string
  description = "Password for Wazuh dashboard kibanaserver user"
}
