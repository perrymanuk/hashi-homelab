job "coredns" {
  region = var.region
  datacenters = ["dc1"]
  type = "service"
  priority = 100

  meta {
      job_file = "nomad_jobs/core-infra/coredns/nomad.job"
      version = "6"
  }

  constraint {
    attribute = "${meta.dns}"
    operator  = "="
    value     = "true"
  }

  group "dns" {
    count = 2
    
    constraint {
      operator = "distinct_hosts"
      value    = "true"
    }
    
    network {
      mode = "host"
      port "dns" {
        static = "53"
        host_network = "lan"
      }
      port "metrics" {
        static = "9153"
        host_network = "lan"
      }
    }

    update {
      max_parallel     = 1
      min_healthy_time = "60s"
      auto_revert      = true
      auto_promote     = true
      canary           = 2
    }

    task "keepalived-dns" {
      driver = "docker"
      
      lifecycle {
        hook = "prestart"
        sidecar = true
      }
      
      config {
        image = "osixia/keepalived:2.0.20"
        network_mode = "host"
        force_pull = false
        volumes = [
          "local/:/container/environment/01-custom"
        ]
        cap_add = ["NET_ADMIN", "NET_BROADCAST", "NET_RAW"]
      }
      
      template {
        destination = "local/env.yaml"
        change_mode = "restart"
        splay       = "1m"
        data        = <<EOH
KEEPALIVED_VIRTUAL_IPS:
  - 192.168.50.2/24
  - 192.168.50.3/24
KEEPALIVED_UNICAST_PEERS:
{{- range service "coredns" }}
  - {{ .Address }}
{{- end }}
KEEPALIVED_INTERFACE: {{ sockaddr "GetPrivateInterfaces | include \"network\" \"192.168.50.0/24\" | attr \"name\"" }}
EOH
      }
      
      resources {
        cpu    = 100
        memory = 32
      }
    }

    task "coredns" {
      driver = "docker"
      
      config {
        image = "coredns/coredns:1.14.0"
        network_mode = "host"
        force_pull = false
        ports = ["dns", "metrics"]
        args = ["-conf", "/local/coredns/corefile"]
      }

      service {
        port = "dns"
        name = "coredns"
        tags = ["coredns"]
        check {
          type     = "tcp"
          interval = "10s"
          timeout  = "2s"
        }
      }
      
      service {
        port = "metrics"
        name = "coredns"
        tags = ["metrics", "coredns"]
      }

      template {
        data = <<EOH
. {
  bind {{ env "NOMAD_IP_dns" }} 192.168.50.2 192.168.50.3
  forward . 8.8.8.8
  log
  errors
  prometheus {{ env "NOMAD_IP_metrics" }}:9153
}
consul.:53 {
  bind {{ env "NOMAD_IP_dns" }} 192.168.50.2 192.168.50.3
  forward . {{ env "NOMAD_IP_dns" }}:8600
  log
  prometheus {{ env "NOMAD_IP_metrics" }}:9153
}
fritz.box.:53 {
  bind {{ env "NOMAD_IP_dns" }} 192.168.50.2 192.168.50.3
  forward . 192.168.50.1:53
  log
  prometheus {{ env "NOMAD_IP_metrics" }}:9153
}
${var.tld}. {
  bind {{ env "NOMAD_IP_dns" }} 192.168.50.2 192.168.50.3

  file /local/coredns/zones/db.home.lab ${var.tld}

}
k8s. {
  bind {{ env "NOMAD_IP_dns" }} 192.168.50.2 192.168.50.3

  file /local/coredns/zones/db.k8s k8s

}
EOH
        destination = "local/coredns/corefile"
        env         = false
        change_mode = "signal"
        change_signal = "SIGHUP"
        left_delimiter  = "{{"
        right_delimiter = "}}"
      }

      template {
        change_mode   = "signal"
        change_signal = "SIGUSR1"
        destination = "local/coredns/zones/db.home.lab"
        data = <<EOH
$ORIGIN ${var.tld}.
$TTL    604800
${var.tld}.         IN SOA	ns1.${var.tld}. admin.${var.tld}. (
         {{ timestamp "unix" }}        ; Serial, current unix timestamp
             604800        ; Refresh
              86400        ; Retry
            2419200        ; Expire
             604800 )      ; Negative Cache TTL

; name servers - NS records
${var.tld}.         IN NS	 ns1.${var.tld}.
${var.tld}.         IN NS	 ns2.${var.tld}.

; name servers - A records
ns1                      IN A   192.168.50.1
ns2                      IN A   192.168.50.2

{{- /*  Point domains to the floating IP from keepalived */}}
; services - A records
lab.${var.tld}.         IN A   192.168.50.2
*                       IN A   192.168.50.2
@                       IN A   192.168.50.2

EOH
      }

      template {
        change_mode   = "signal"
        change_signal = "SIGUSR1"
        destination = "local/coredns/zones/db.k8s"
        data = <<EOH
$ORIGIN ${var.tld}.
$TTL    604800
k8s.         IN SOA	ns1.k8s. admin.k8s. (
         {{ timestamp "unix" }}        ; Serial, current unix timestamp
             604800        ; Refresh
              86400        ; Retry
            2419200        ; Expire
             604800 )      ; Negative Cache TTL

; name servers - NS records
k8s.         IN NS	 ns1.k8s.
k8s.         IN NS	 ns2.k8s.

; name servers - A records
ns1                      IN A   192.168.50.1
ns2                      IN A   192.168.50.2

{{- /*  Point domains to the floating IP from keepalived */}}
; services - A records
lab.k8s.                IN A   192.168.50.93
*.k8s.                  IN A   192.168.50.93
@                       IN A   192.168.50.93

EOH
      }
      
      resources {
        cpu    = 100
        memory = 128
      }
    }
  }
}

variable "region" {}
variable "tld" {}