job "keepalived" {
  datacenters = ["dc1"]
  type        = "system"
  priority    = 100

  meta {
      job_file = "nomad_jobs/core-infra/keepalived/nomad.job"
version = "5"
  }

  group "keepalived" {

    restart {
      attempts = 3
      delay    = "15s"
      interval = "10m"
      mode     = "delay"
    }

    update {
      max_parallel     = 1
      min_healthy_time = "30s"
      auto_revert      = true
    }

    task "keepalived" {
      driver = "docker"
      config {
        image = "osixia/keepalived:2.0.20"
        network_mode = "host"
        volumes = [
            "local/:/container/environment/01-custom"
        ]
        cap_add = ["NET_ADMIN", "NET_BROADCAST", "NET_RAW"]
      }
      template {
        destination = "local/env.yaml"
        change_mode = "restart"
        splay       = "1m"
        data        = <<EOH
KEEPALIVED_VIRTUAL_IPS:
  - 192.168.50.2/24
  - 192.168.50.3/24
KEEPALIVED_UNICAST_PEERS:
{{- with $node := node -}}
{{ range nodes }}
{{- if ne .Address $node.Node.Address }}
  - {{ .Address }}
{{- end -}}
{{- end -}}
{{- end }}
KEEPALIVED_INTERFACE: {{ sockaddr "GetPrivateInterfaces | include \"network\" \"192.168.50.0/24\" | attr \"name\"" }}
EOH
      }
      resources {
        cpu    = 100
        memory = 32
      }
    }
  }
}
